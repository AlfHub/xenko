// Copyright (c) 2014-2017 Silicon Studio Corp. All rights reserved. (https://www.siliconstudio.co.jp)
// See LICENSE.md for full license information.

namespace SiliconStudio.Xenko.Rendering.Images
{
    shader LightShaftsShader<int SampleCount, bool Animated> : ImageEffectShader, ShadowMapCommon<PerFrame>, PostEffectBoundingRay<SampleCount, Animated>, LightStream, NormalStream
    {
        stage compose DirectLightGroup lightGroup;

        cbuffer PerFrame
        {
            stage float ExtinctionFactor;
            stage float DensityFactor;
            stage float ExtinctionRatio;
        };

        override float ComputeColorIn(float4 positionWS, float stepSize, int stepIndex)
        {
            float3 lightResult = 0;
            float3 sigmaT = ComputeDensity(positionWS) * ExtinctionFactor;
            float3 sigmaS = sigmaT * ExtinctionRatio;
            float3 stepExtinction = exp(-sigmaT * stepSize);

            // Most shadow groups use these for normal scaled bias
            ResetLightStream();
            streams.NdotL = 1;
            streams.normalWS = float3(0,1,0);

            float atten = lightGroup.ComputeAttenuation(positionWS.xyz, 0);
            float3 shadowColor = lightGroup.ComputeShadow(positionWS.xyz, 0);

            // Right now this doesn't support multi-colored shadows, since this shader only calculates the light shaft intensity, which is later multiplied by the light color
            // So take the max here
            float shadow = max(max(shadowColor.x, shadowColor.y), shadowColor.z);

            lightResult = stepExtinction * sigmaS * stepSize * shadow * atten;
            return lightResult;
        }

        override float ComputeColorOut()
        {
            return 0.0f;
        }

        float ComputeDensity(float positionVS)
        {
            return DensityFactor;
        }
    };
}