// Copyright (c) 2014-2017 Silicon Studio Corp. All rights reserved. (https://www.siliconstudio.co.jp)
// See LICENSE.md for full license information.
namespace SiliconStudio.Xenko.Rendering.Materials
{
    /// <summary>
    /// Material glossiness map
    /// </summary>
    shader MaterialSurfaceGlossinessMapMetalFlakes<bool TInvert> : IMaterialSurfacePixel,
                                                                    MaterialPixelShadingStream,
                                                                    Transformation,
                                                                    PositionStream4
    {
        compose ComputeColor glossinessMap;
        compose ComputeColor baseGlossinessMap;

        override void Compute()
        {
            var glossiness = glossinessMap.Compute().r;
            var baseGlossiness = baseGlossinessMap.Compute().r;
            if (TInvert)
            {
                glossiness = 1.0 - glossiness;
                baseGlossiness = 1.0 - baseGlossiness;
            }
            
            // Correct both glossiness factor (to avoid aliasing and unrealistic values)
            float normalLength = length(streams.matNormal);
            glossiness = normalLength * glossiness / (normalLength + glossiness * (1.0f - normalLength));
            baseGlossiness = normalLength * baseGlossiness / (normalLength + baseGlossiness * (1.0f - normalLength));
            
            // Interpolate the factors using the surface to camera distance
            float LOD = saturate(distance(Eye.xyz, streams.PositionWS.xyz) * 2.0f);
            streams.matGlossiness = lerp(glossiness, baseGlossiness, LOD);
        }
    };
}
